module Grafcet2PetriNet;
create OUT : PetriNet from IN : Grafcet;

-- The PetriNet rule generates a PetriNet element from the input Grafcet element. 
-- Name of the generated PetriNet element is copied from the one of the input Grafcet.
-- Its set of Elements corresponds to Elements generated by Place and Transition rules.
-- And its set of Arcs corresponds to Connections generated by PlaceToTransition and TransitionToPlace rules.
rule PetriNet {
	from
		g : Grafcet!Grafcet
	to
		p : PetriNet!PetriNet
		(
			location <- g.location,
			name <- g.name,
			elements <- g.elements,
			arcs <- g.connections
		)
}

-- The Place rule generates a Place element from the input Step element. 
-- Name of the generated Place element is copied from the one of the input Step.
-- Its set of incomingArcs corresponds to incomingConnections generated by TransitionToPlace rule.
-- And its set of outgoingArc corresponds to outgoingConnections generated by PlaceToTransition rule.
rule Place {
	from
		g : Grafcet!Step
	to
		p : PetriNet!Place
		(
			location <- g.location,
			name <- g.name,
			net <- g.grafcet,
			incomingArc <- g.incomingConnections,
			outgoingArc <- g.outgoingConnections
		)
}

-- The Transition rule generates a Transition element from the input Transition element. 
-- Name of the generated Transition element is copied from the one of the input Transition.
-- Its set of incomingArcs corresponds to incomingConnections generated by PlaceToTransition rule.
-- And its set of outgoingArc corresponds to outgoingConnections generated by TransitionToPlace rule.
rule Transition {
	from
		g : Grafcet!Transition
	to
		p : PetriNet!Transition
		(
			location <- g.location,
			name <- g.name,
			net <- g.grafcet,
			incomingArc <- g.incomingConnections,
			outgoingArc <- g.outgoingConnections
		)
}

-- The PlaceToTransition rule generates a PlaceToTransition element from the input StepToTransition element. 
-- Name of the generated PlaceToTransition element is copied from the one of the input StepToTransition.
-- Its from and to references are also copied from the ones of the input StepToTransition.
rule PlaceToTransition {
	from
		g : Grafcet!StepToTransition
	to
		p : PetriNet!PlaceToTransition
		(
			location <- g.location,
			name <- g.name,
			net <- g.grafcet,
			"from" <- g."from",
			"to" <- g."to"
		)
}

-- The TransitionToPlace rule generates a TransitionToPlace element from the input TransitionToStep element. 
-- Name of the generated TransitionToPlace element is copied from the one of the input TransitionToStep.
-- Its from and to references are also copied from the ones of the input TransitionToStep.
rule TransitionToPlace {
	from
		g : Grafcet!TransitionToStep
	to
		p : PetriNet!TransitionToPlace
		(
			location <- g.location,
			name <- g.name,
			net <- g.grafcet,
			"from" <- g."from",
			"to" <- g."to"
		)
}
