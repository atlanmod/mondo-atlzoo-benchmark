module PetriNet2Grafcet;
create OUT : Grafcet from IN : PetriNet;

-- The Grafcet rule generates a Grafcet element from the input Petri Net element. 
-- Name of the generated Grafcet element is copied from the one of the input Petri Net.
-- Its set of Elements corresponds to Elements generated by Step and Transition rules.
-- And its set of Connections corresponds to Arcs generated by StepToTransition and TransitionToStep rules.
rule Grafcet {
	from
		p : PetriNet!PetriNet
		
	to	g : Grafcet!Grafcet
		(
			location <- p.location,
			name <- p.name,
			elements <- p.elements,
			connections <- p.arcs
		)
}

-- The Step rule generates a Step element from the input Place element. 
-- Name of the generated Step element is copied from the one of the input Place.
-- Its set of incomingConnections corresponds to incomingArcs generated by TransitionToStep rule.
-- And its set of outgoingConnections corresponds to outgoingArc generated by StepToTransition rule.
rule Step {
	from
		p : PetriNet!Place
	to
		g : Grafcet!Step
		(
			location <- p.location,
			name <- p.name,
			grafcet <- p.net,
			isInitial <- false,
			isActive <- false,
			incomingConnections <- p.incomingArc,
			outgoingConnections <- p.outgoingArc
		)
}

-- The Transition rule generates a Transition element from the input Transition element. 
-- Name of the generated Transition element is copied from the one of the input Transition.
-- Its set of incomingConnections corresponds to incomingArcs generated by StepToTransition rule.
-- And its set of outgoingConnections corresponds to outgoingArc generated by TransitionToStep rule.
rule Transition {
	from
		p : PetriNet!Transition
		
	to
		g : Grafcet!Transition
		(
			location <- p.location,
			name <- p.name,
			grafcet <- p.net,
			incomingConnections <- p.incomingArc,
			outgoingConnections <- p.outgoingArc
		)
}

-- The StepToTransition rule generates a StepToTransition element from the input PlaceToTransition element. 
-- Name of the generated StepToTransition element is copied from the one of the input PlaceToTransition.
-- Its from and to references are also copied from the ones of the input PlaceToTransition.
rule StepToTransition {
	from
		p : PetriNet!PlaceToTransition
	to
		g : Grafcet!StepToTransition
		(
		 	location <- p.location,
			name <- p.name,
			grafcet <- p.net,
			"from" <- p."from",
			"to" <- p."to"
		)
}

-- The TransitionToStep rule generates a TransitionToStep element from the input TransitionToPlace element. 
-- Name of the generated TransitionToStep element is copied from the one of the input TransitionToPlace.
-- Its from and to references are also copied from the ones of the input TransitionToPlace.
rule TransitionToStep {
	from
		p : PetriNet!TransitionToPlace
	to
		g : Grafcet!TransitionToStep
		(
		 	location <- p.location,
			name <- p.name,
			grafcet <- p.net,
			"from" <- p."from",
			"to" <- p."to"
		)
}
